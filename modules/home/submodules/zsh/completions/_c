#compdef _c c

_c() {
  local nix_base="$HOME/.config/nix"
  local home_base="${nix_base}/modules/home"
  local -a commands

  commands=(
    "zsh:configure zsh"
    "z:configure zsh"
    "git:configure git"
    "g:configure git"
    "vim:configure neovim"
    "v:configure neovim"
    "nix:configure nix"
    "n:configure nix"
    "home_manager:configure home_manager"
    "hm:configure home_manager"
    "h:configure home_manager"
    "scripts: edit scripts"
    "s: edit scripts"
    "completions: edit completions"
    "c: edit completions"
  )

  local curcontext="$curcontext" state line
  local -A opt_args

  _arguments -C \
    ':command:->command' \
    '*::files:->files'

  case "$state" in
    command)
      _describe -t commands 'commands' commands
      ;;
    files)
      case $line[1] in
        n | nix)
          _path_files -W "${nix_base}"
          ;;
        h | hm | home_manager)
          _path_files -W "${home_base}"
          ;;
        z | zsh)
          _path_files -W "${home_base}/submodules/zsh"
          ;;
        g | git)
          _path_files -W "${home_base}/submodules/git"
          ;;
        v | vim)
          _path_files -W "${home_base}/submodules/vim"
          ;;
        s | scripts)
          _path_files -W "${home_base}/submodules/zsh/scripts"
          ;;
        c | completions)
          _path_files -W "${home_base}/submodules/zsh/completions"
          ;;
        *)
          _message "command not supported $@"
          ;;
      esac
      ;;
    *) ;;
  esac
  return 0
}
_c
